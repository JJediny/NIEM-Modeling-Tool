/*
 * This program and the accompanying materials are made available
 * under the terms of the Eclipse Public License v1.0 which
 * accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   SEARCH Group, Incorporated - initial API and implementation
 *
 */
pre {
    var validator = new Native('org.search.niem.uml.evl.validation.NiemValidationTool');
}

context Model {

    guard : self.hasPimProfile()
    
	critique ContainsExchange {
		check : self.nestedPackages->exists(p|p.hasExchange())
		message : validator.getMessage('_UI_critique_no_exchange_package')
	}
}

context Package {

    guard : self.isExchange()
    
    critique ContainsRootElement {
        check : self.ownedTypes->exists(t|t.hasRootElement())
        message : validator.getMessage('_UI_critique_no_root_element', self.name)
    }
}

operation Type hasRootElement() : Boolean {
    return self.isKindOf(Class) and self.appliedStereotypes->exists(s|s.isPropertyHolder()) and self.ownedElements->exists(e|e.isKindOf(Property));
}

operation Package hasExchange() : Boolean {
    return self.isExchange() or self.nestedPackages->exists(p|p.hasExchange());
}

operation Package hasPimProfile() : Boolean {
    return self.appliedProfiles->exists(p|p.isPimProfile()) or self.nestedPackages->exists(p|p.hasPimProfile());
}

operation Package isExchange() : Boolean {
    var defaultPurpose:EnumerationLiteral=self.getDefaultPurpose();
    return defaultPurpose.isDefined() and defaultPurpose.name = 'exchange';
}

operation Package getDefaultPurpose() : EnumerationLiteral {
    var appliedInformationModel:Stereotype=self.appliedStereotypes.selectOne(s|s.isInformationModel());
    if(appliedInformationModel.isDefined()) {
        return self.getValue(appliedInformationModel, 'defaultPurpose');
    }
    return null;
}

operation Stereotype isPropertyHolder() : Boolean {
    return validator.isPropertyHolder(self.qualifiedName);
}

operation Stereotype isInformationModel() : Boolean {
    return validator.isInformationModel(self.qualifiedName);
}

operation Profile isPimProfile() : Boolean {
    return validator.isPimProfile(self.qualifiedName);
}